#!/usr/bin/perl
use strict;
use warnings;

sub sqlplusExec();
sub readSecrets();
sub run(@);

my $secretsFile = "$ENV{HOME}/.secrets";

my $urls = {
  ehrdev => 'ehr-db.dev',
  ehrstg => 'ehr-db.stage',
};
my $okSids = join "|", sort keys %$urls;
my $port = 2483;

my $defaultSid = "ehrdev";
my $defaultSchema = "escribe";

my $usage = "Usage:
  $0 [$okSids]
    connect to indicated db sid {default is $defaultSid}
";

sub main(@){
  my $sid = shift;
  $sid = $defaultSid if not defined $sid;
  die $usage if @_ > 0 or $sid !~ /^($okSids)$/;
  my $schema = $defaultSchema;

  my $dbs = readSecrets;
  my $pw = $$dbs{$sid}{$schema};
  my $url = $$urls{$sid};
  die "missing password for sid=$sid and schema=$schema\n" if not defined $pw;
  die "Unknown url for sid=$sid\n" if not defined $url;

  my $sqlplus = sqlplusExec();
  run $sqlplus, "$schema/$pw\@$url:$port/$sid";
}

sub sqlplusExec(){
  my @execs = qw(gqlplus sqlplus);
  my @paths;
  push @paths, map {"./$_"} @execs;
  push @paths, map {$_=`which $_`; chomp; $_} @execs;

  for my $path(@paths){
    return $path if -e $path and -x $path;
  }
  return undef;
}

sub readSecrets(){
  my @lines = `cat $secretsFile`;
  my $dbs = {};
  for my $line(@lines){
    if($line =~ /^oracle\.([^\.]+)\.([^\.]+)\s*=\s*(.+)$/){
      my ($sid, $schema, $pw) = ($1, $2, $3);
      $$dbs{$sid} = {} if not defined $$dbs{$sid};
      $$dbs{$sid}{$schema} = $pw;
    }
  }
  return $dbs;
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
