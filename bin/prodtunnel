#!/usr/bin/perl
use strict;
use warnings;

my $tunnels = {
  prod        => ["ehr.prod",             "ehr-db.prod",          2483],
  oldprod     => ["garuda.escribe.com",   "garuda.escribe.com",   1521],
  olddatadict => ["datadict.escribe.com", "datadict.escribe.com", 1521],
};
my $okTunnels = join "|", sort keys %$tunnels;

my $user = 'ewolk';
my $controlFile = '/tmp/prodtunnelssh.ctl';

sub createTunnel($$$);
sub run(@);

my $usage = "Usage:
  $0 off
    ssh -S $controlFile -O exit localhost
  $0 $okTunnels
    ssh -M -S $controlFile -f $user\@<HOST> -L <PORT>:<DEST_HOST>:<PORT> -N
";

sub main(@){
  if(@_ == 1 and defined $$tunnels{$_[0]}){
    my $tunnel = $$tunnels{$_[0]};
    my ($host, $destHost, $port) = @$tunnel;
    createTunnel $host, $destHost, $port;
  }elsif(@_ == 1 and $_[0] eq "off"){
    run 'ssh', '-S', $controlFile, '-O', 'exit', 'localhost';
  }else{
    die $usage;
  }
}

sub createTunnel($$$){
  my ($host, $destHost, $port) = @_;
  die "Control file already exists, run $0 off" if -e $controlFile;
  run(
    'ssh',
    '-M',
    '-S', $controlFile,
    '-f',
    "$user\@$host",
    '-L', "$port:$destHost:$port",
    "-N",
  );
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
