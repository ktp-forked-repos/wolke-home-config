#!/usr/bin/perl
use strict;
use warnings;

my $usage = "Usage:
  $0 [OPTS]
    print \"<WIDTH>x<HEIGHT>\" of the primary display
    uses xrandr

  OPTS:
    -w | --width | WIDTH | width
      print just <WIDTH>
    -h | --height | HEIGHT | height
      print just <WIDTH>
    --percent=PERCENT
      multiply the <WIDTH> and/or <HEIGHT> by PERCENT,
        divide by 100,
        and round to the nearest pixel before printing
      <PERCENT> can be any non-negative rational number
      e.g.: `$0 -p 50` prints \"960x540\" on 1920x1080 screen
";

sub main(@){
  my $showWidth = 1;
  my $showHeight = 1;
  my $percent = 100;
  while(@_ > 0){
    my $opt = shift;
    if($opt =~ /^(-w|--width|WIDTH|width)$/){
      $showWidth = 1;
      $showHeight = 0;
    }elsif($opt =~ /^(-h|--height|HEIGHT|height)$/){
      $showWidth = 0;
      $showHeight = 1;
    }elsif($opt =~ /^--percent=(\d+(?:\.\d+)?)$/){
      $percent = $1;
    }else{
      die $usage;
    }
  }

  my $xrandr = `xrandr`;
  die "Error running xrandr\n" if $? != 0;

  my ($width, $height);
  if($xrandr =~ /^\s*(\d+)x(\d+).*\*/m){
    ($width, $height) = ($1, $2);
  }elsif($xrandr =~ /current\s+(\d+)\s*x\s*(\d+),/){
    ($width, $height) = ($1, $2);
  }else{
    die "Could not parse xrandr output:\n$xrandr\n";
  }

  $width = int($width * $percent / 100 + 0.5);
  $height = int($height * $percent / 100 + 0.5);

  my $fmt = "";
  $fmt .= $width if $showWidth;
  $fmt .= "x" if $showWidth and $showHeight;
  $fmt .= $height  if $showHeight;

  print "$fmt\n";
}

&main(@ARGV);
