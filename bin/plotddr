#!/usr/bin/perl
use strict;
use warnings;

sub run(@);

my $gnuplotDir = "$ENV{HOME}/.cache/gnuplot-ddr";
my $bestDir = "$ENV{HOME}/.cache/best-ddr";
my $pixmirrorDir = "$ENV{HOME}/Desktop/DCIM-pixmirror/xddr";

my @curSongs = qw(
  max300 legendofmax maxxunlimited max300smmm
  paranoiasurvivor270 paranoiasurvivormax290 paranoiarespect
  trim tripmachinephoenix healingdvision
);
my $curSongsRegex = "^(" . join("|", @curSongs) . ")\$";

my @plotArr = (
  'cursongs_recent' => {
    filters => ["--song", "$curSongsRegex", "--recent", "--singles", "--monotonic"],
    formatOpts => ["--notes", "highscore"],
    plotOpts => [],
  },
  'cursongs_recent_smooth' => {
    filters => ["--song", "$curSongsRegex", "--recent", "--singles"],
    formatOpts => ["--notes", "highscore"],
    plotOpts => ["--smooth"],
  },
  'hard_recent' => {
    filters => ["--hard", "--recent", "--singles", "--monotonic"],
    formatOpts => ["--notes", "highscore"],
    plotOpts => [],
  },
  'hard_recent_smooth' => {
    filters => ["--hard", "--recent", "--singles"],
    formatOpts => ["--notes", "highscore"],
    plotOpts => ["--smooth"],
  },
  'high' => {
    filters => ["--score", "870000", "--singles", "--monotonic"],
    formatOpts => ["--notes", "highscore,extra"],
    plotOpts => [],
  },
  'doubles' => {
    filters => ["--recent", "--doubles", "--monotonic"],
    formatOpts => ["--notes", "highscore"],
    plotOpts => [],
  },
);
my $plots = {@plotArr};
my @plotOrder = map {$plotArr[$_]} grep {$_%2 == 0} (0 .. $#plotArr);

my $subdirVariants = {
  'fhd' => {
    extraFilters => [],
    extraPlotOpts => ["--size", "1920x1080"],
    extraFormatOpts => [],
  },
  'n9' =>  {
    extraFilters => [],
    extraPlotOpts => ["--size", "854x480"],
    extraFormatOpts => ["--abbrev"],
  },
};

my $usage = "Usage: $0 [--nofeh|--non9]\n";

sub main(@){
  my $noFeh = 0;
  my $noN9 = 0;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^--nofeh/){
      $noFeh = 1;
    }elsif($arg =~ /^--non9/){
      $noN9 = 1;
    }else{
      die $usage;
    }
  }
  die $usage if @_ > 0;

  system "rm", "-rf", $gnuplotDir;
  for my $subdir(sort keys %$subdirVariants){
    system "mkdir", "-p", "$gnuplotDir/$subdir";
    my $variant = $$subdirVariants{$subdir};
    my $num = 0;
    my @order = @plotOrder;
    @order = reverse @order if $subdir eq 'n9';
    for my $name(@order){
      $num++;
      my $plot = $$plots{$name};
      my @filters = (@{$$plot{filters}}, @{$$variant{extraFilters}});
      my @formatOpts = (@{$$plot{formatOpts}}, @{$$variant{extraFormatOpts}});
      my @plotOpts = (@{$$plot{plotOpts}}, @{$$variant{extraPlotOpts}});

      my $file = "$gnuplotDir/$subdir/$num-gnuplot-$name.png";
      @plotOpts = ("--file", $file, @plotOpts);
      my @cmd = ("ddrname", "--gnuplot", @plotOpts, @formatOpts, @filters);

      run @cmd;
      die "error running plot $name => @$plot\n" if $? != 0;
    }
  }

  system "rm", "-rf", $bestDir;
  my @bestNotHard;
  my @bestHard;
  my @bestDoubles;

  @bestHard = (@bestHard, `ddrname --bestfiles --hard --singles`);
  @bestHard = (@bestHard, `ddrname --bestfiles --nonstop`);

  @bestNotHard = (@bestNotHard, `ddrname --bestfiles --nothard --singles`);

  @bestDoubles = (@bestDoubles, `ddrname --bestfiles --doubles`);
  @bestDoubles = (@bestDoubles, `ddrname --bestfiles --nonstopdoubles`);

  chomp foreach @bestHard;
  chomp foreach @bestNotHard;
  chomp foreach @bestDoubles;

  @bestHard = map { "$pixmirrorDir/$_" } @bestHard;
  @bestNotHard = map { "$pixmirrorDir/$_" } @bestNotHard;
  @bestDoubles = map { "$pixmirrorDir/$_" } @bestDoubles;

  system "mkdir", "-p", "$bestDir/hard", "$bestDir/nothard", "$bestDir/doubles";
  system "cp", "-ar", @bestHard, "$bestDir/hard";
  system "cp", "-ar", @bestNotHard, "$bestDir/nothard";
  system "cp", "-ar", @bestDoubles, "$bestDir/doubles";

  if(not $noFeh){
    run "feh -ZF '$gnuplotDir/fhd' >/dev/null 2>/dev/null &";
  }
  if(not $noN9){
    my $n9Host = `n9`;
    chomp $n9Host;

    run "rsync", "-avP", "--del", "$gnuplotDir/n9/", "user\@$n9Host:~/xgnuplotddr";
    run "rsync", "-avP", "--del", "$bestDir/", "user\@$n9Host:~/xbestddr";
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
