#!/usr/bin/perl
use strict;
use warnings;
use Time::HiRes qw(sleep);

sub guessDisplay($);

my $defaultDisplay = ":0";
my $localhost = "127.0.0.1";
my $port = '5900';
my $delayMillis = 2500;
my $sshControlFile = '/tmp/vncSSH.ctl';

my @server = (
  "x11vnc",
    "-safer", "-localhost", "-nopw", "-once",
    "-noncache",
);

my %clients = (
  xtigervncviewer => [
    "$localhost:0",
  ],
  xtightvncviewer => [
    "$localhost:0",
    "-quality", "0",
    "-compresslevel", "5",
  ],
  x2vnc => [
    "-hotkey", "a-F9",
    "-edgewidth", "0",
    "$localhost:0",
  ],
);

my $defaultClient = "xtigervncviewer";
my $okClients = join "|", sort keys %clients;

my @sshBind = (
  "ssh", "-f",
    "-S", $sshControlFile,
    "-L", "$port:$localhost:$port",
);
my @sshExit = (
  "ssh",
    "-S", $sshControlFile,
    "-O", "exit",
    "$localhost",
);

my $usage = "Usage:
  $0 [OPTS] HOST
    start x11vnc on the HOST, setup an ssh tunnel and then connect to it
    if --x2vnc is given, client is x2vnc. otherwise, client is vncviewer

  $0 -h | --help
    show this message

  $0 off
    disconnect ssh tunnel

  OPTS
    --client=CLIENT   (default is $defaultClient)
      one of: $okClients
    --geometry=GEOMETRY    (default is --geometry=res)
      one of 'res', 'none', or <WIDTH>x<HEIGHT>
        if <GEOMETRY> is 'res':
          fetch <WIDTH>x<HEIGHT> with `res` command
          and pass '-geometry <WIDTH>x<HEIGHT>' to server
        if <GEOMETRY> is <WIDTH>x<HEIGHT>:
          pass '-geometry <WIDTH>x<HEIGHT>' to server
        if <GEOMETRY> is 'none':
          do not pass '-geometry' to server
    --no-geometry
      synonym for --geometry=none (do not pass '-geometry GEOMETRY' to server)
";

sub main(@){
  if(@_ == 1 and $_[0] =~ /^(-h|--help)/){
    print $usage;
    exit 0;
  }elsif(@_ == 1 and $_[0] eq 'off'){
    exec @sshExit;
  }

  my $client = $defaultClient;
  my $geometry = "res";
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^--client=($okClients)$/){
      $client = $1;
    }elsif($arg =~ /^--geometry=(res|none|\d+x\d+)$/){
      $geometry = $1;
    }
  }

  my $host = shift if @_ > 0;

  die $usage if not defined $host or @_ > 0;

  if($geometry eq "res"){
    $geometry = `res`;
    chomp $geometry;
  }

  if($geometry !~ /^(none|\d+x\d+)$/){
    die "invalid geometry: $geometry\n";
  }

  system @sshExit;
  system "ssh", $host, "pkill x11vnc";

  @server = (@server, '-display', guessDisplay $host);
  if($geometry =~ /^(\d+x\d+)$/){
    @server = (@server, "-geometry", $geometry);
  }

  system @sshBind, $host, "@server";
  exit $? if $? != 0;

  sleep($delayMillis/1000.0);

  my @clientArgs = @{$clients{$client}};
  system $client, @clientArgs;
}

sub guessDisplay($){
  my $host = shift;
  my $who = `ssh "$host" who`;
  if($who =~ /\s*\((:\d+)\)$/m){
    return $1;
  }
  return $defaultDisplay;
}

&main(@ARGV);
