#!/usr/bin/perl
use strict;
use warnings;

sub run(@);

my $browser = "firefox";
my @preCmd = ("sudo", "modprobe", "-r", "uvcvideo");
my @postCmd = ("pkill", "-f", "Google");

my $baseUrl = "https://plus.google.com/hangouts";

my %hangouts = (
  lille           => "$baseUrl/_/event/cu9g1kdmmt26o2l3c938i3s3nag",
);
my @hangoutNames = sort keys %hangouts;
my $okHangouts = join "|", @hangoutNames;
my $hangoutFmt = join "      ", map {"$_ => $hangouts{$_}\n"} @hangoutNames;
my $defaultHangout = "lille";

my $usage = "Usage:
  $0 [HANGOUT_NAME|URL]
    -run @preCmd
    -open $browser with URL for given HANGOUT_NAME (or URL indicated explicitly)
      {default is $defaultHangout =>\n        $hangouts{$defaultHangout}}
    -run @postCmd

  $0 -p|-u|--print|--url [HANGOUT_NAME|URL]
    -print URL for given HANGOUT_NAME (or URL indicated explicitly)
      {default is $defaultHangout =>\n        $hangouts{$defaultHangout}}

  HANGOUT_NAME
    word for predefined hangout URL as below (HANGOUT_NAME => URL):
      $hangoutFmt
";

sub main(@){
  my $printOnly = 0;
  while(@_ > 0 and $_[0] =~ /^-/){
    my $arg = shift;
    if($arg =~ /^(-p|-u|--print|--url)$/){
      $printOnly = 1;
    }else{
      die $usage;
    }
  }

  my $hangoutArg = shift;
  $hangoutArg = $defaultHangout if not defined $hangoutArg;

  die $usage if @_ > 0;

  my $url;
  if($hangoutArg =~ /^($okHangouts)$/){
    $url = $hangouts{$hangoutArg};
  }elsif($hangoutArg =~ /^https?:\/\//){
    $url = $hangoutArg;
  }else{
    die $usage;
  }

  if($printOnly){
    print "$url\n";
  }else{
    run @preCmd;
    run $browser, $url;
    run @postCmd;
  }
}

sub run(@){
  print "@_\n";
  system @_;
}

&main(@ARGV);
